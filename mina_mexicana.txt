$${
// Mina Mexicana, por lomexicano;
// Versão 1.0: 06/01/2025;
// Inicie segurando CTRL para configurar!	;
//
// FUNÇÕES:
// -> Atacar blocos na mira, automaticamente (apenas com durabilidade aceitável);
// -> Contagem de blocos quebrados;
// -> Auto-Reparar (default: OFF);
// -> Auto-Sprint (default: ON);
// -> Auto-Pot de velocidade (default: ON);
// -> Auto-Home (default: ON);
// -> Aviso de PvP iniciando (default: ON);
// -> Auto-"/pvp off" ao iniciar a macro (default: OFF);
// -> Aviso de hotbar cheia (default: ON);
// -> Mostrar contagem de blocos quberados no chat (default: OFF);
// -> Informações na tela (criar labels, caso queira, conforme descrição abaixo);
//
//
////////////////// ////////////////// ;
// LABELS 
// -> "horario"		--> Com a hora, status de PvP e avisos;
// -> "efeitos"		--> Com os efeitos de poções ativos;
// -> "rendimento"	--> Com os rendimentos e espaços livres na hotbar;
// 
// 
// Para criar uma label:
// -> Abra o teclado de macros;
// -> vá em Gui Editor;
// -> Clique duas vezes em inGame, no menu à esquerda; 
// -> Clique no "+", no canto inferior esquerdo, e em "Add Label";
// -> No primeiro campo, coloque o nome da label, conforme lista acima. Salve.
////////////////// ////////////////// ;

log(" ");
toggle(@&mina_mexicana[0]);

if(@&mina_mexicana[0] == "false");
	setlabel("horario","");
	setlabel("efeitos","");
	setlabel("rendimento","");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &f&lMINA &2&lMEX&f&lIC&4&lANA&f&l: &c&lOFF&4&l!");
	stop("MINA_MEXICANA");
	stop();
endif;
	
log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &f&lMINA &2&lMEX&f&lIC&4&lANA&f&l: &a&lON&2&l!");

#hora_dia = 6;		// hora, no DAYTIME, que o PvP fica OFF;
#minuto_dia = 0;	// minuto, no DAYTIME, que o PvP fica OFF;
#hora_noite = 19;	// hora, no DAYTIME, que o PvP fica ON;
#minuto_noite = 0;	// minuto, no DAYTIME, que o PvP fica ON;

unset(versao_152);
ifmatches("%ITEM%","^\d+$");
	set(versao_152);
endif;

// Índice 1: ids para quebrar;
ifmatches("%@&mina_mexicana[1]%","^(?:\w+(?::\d+)?-?)+$",&ids_para_quebrar);
else;
	set(&ids_para_quebrar,"emerald_ore-diamond_ore-coal_ore");
	if(versao_152);
		set(&ids_para_quebrar,"129-56-16");
	endif;
	set(@&mina_mexicana[1],"%&ids_para_quebrar%");
endif;

// Índice 2: id da ferramenta;
ifmatches("%@&mina_mexicana[2]%","^\w+(?::\d+)?$",&id_ferramenta);
else;
	set(&id_ferramenta,"diamond_pickaxe");
	if(versao_152);
		set(&id_ferramenta,"278");
	endif;
	set(@&mina_mexicana[2],"%&id_ferramenta%");
endif;

// Índice 3: durabilidade aceitável;
ifmatches("%@&mina_mexicana[3]%","^\d+$",#durabilidade);
	if(#durabilidade < 10);
		set(#durabilidade,10);
		set(@&mina_mexicana[3],"%#durabilidade%");
	endif;
else;
	set(#durabilidade,150);
	set(@&mina_mexicana[3],"%#durabilidade%");
endif;

// Índice 4: utilizar comando para reparar;
ifmatches("%@&mina_mexicana[4]%","^(true|false)$",reparar_com_comando);
else;
	unset(reparar_com_comando);
	set(@&mina_mexicana[4],"%reparar_com_comando%");
endif;

// Índice 5: avisar hotbar cheia;
ifmatches("%@&mina_mexicana[5]%","^(true|false)$",avisar_hotbar_cheia);
else;
	set(avisar_hotbar_cheia);
	set(@&mina_mexicana[5],"%avisar_hotbar_cheia%");
endif;

// Índice 6: avisar PvP iniciando;
ifmatches("%@&mina_mexicana[6]%","^(true|false)$",avisar_pvp);
else;
	set(avisar_pvp);
	set(@&mina_mexicana[6],"%avisar_pvp%");
endif;

// Índice 7: comando reparar;
ifmatches("%@&mina_mexicana[7]%","^\S+$",&comando_reparar);
else;
	set(&comando_reparar,"/reparar");
	set(@&mina_mexicana[7],"%&comando_reparar%");
endif;

// Índice 8: fugir do PvP;
ifmatches("%@&mina_mexicana[8]%","^(true|false)$",fugir_do_pvp);
else;
	set(fugir_do_pvp);
	set(@&mina_mexicana[8],"%fugir_do_pvp%");
endif;

// Índice 9: comando de fuga;
ifmatches("%@&mina_mexicana[9]%","^\S+$",&comando_de_fuga);
else;
	set(&comando_de_fuga,"/minerar");
	set(@&mina_mexicana[9],"%&comando_de_fuga%");
endif;

// Índice 10: contagem de blocos quebrados, separados por id (padrão " id:damage-quantidade");
ifmatches("%@&mina_mexicana[10]%","^(?: \w+(?::\d+)?-\d+)+$",&blocos_quebrados);
else;
	set(&blocos_quebrados," 0-0");
	set(@&mina_mexicana[10],"%&blocos_quebrados%");
endif;

// Índice 11: auto "/pvp off" ao entrar na mina;
ifmatches("%@&mina_mexicana[11]%","^(true|false)$",auto_pvp_off);
else;
	unset(auto_pvp_off);
	set(@&mina_mexicana[11],"%auto_pvp_off%");
endif;

// Índice 12: mostrar quebrados no chat;
ifmatches("%@&mina_mexicana[12]%","^(true|false)$",mostrar_quebrados_no_chat);
else;
	unset(mostrar_quebrados_no_chat);
	set(@&mina_mexicana[12],"%mostrar_quebrados_no_chat%");
endif;

// Índice 13: quantos segundos antes de anoitecer deve fugir;
ifmatches("%@&mina_mexicana[13]%","^(\d+)$",#fugir_segundos);
else;
	set(#fugir_segundos,15);
	set(@&mina_mexicana[13],"%#fugir_segundos%");
endif;

// Índice 14: quantos segundos antes de anoitecer deve começar a avisar;
ifmatches("%@&mina_mexicana[14]%","^(\d+)$",#aviso_segundos);
else;
	set(#aviso_segundos,30);
	set(@&mina_mexicana[14],"%#aviso_segundos%");
endif;

// Índice 15: quantos segundos entre os avisos;
ifmatches("%@&mina_mexicana[15]%","^(\d+)$",#intervalo_aviso_segundos);
else;
	set(#intervalo_aviso_segundos,2);
	set(@&mina_mexicana[15],"%#intervalo_aviso_segundos%");
endif;

// Índice 16: tomar pot de velocidade;
ifmatches("%@&mina_mexicana[16]%","^(true|false)$",tomar_pot);
else;
	set(tomar_pot);
	set(@&mina_mexicana[16],"%tomar_pot%");
endif;

// Índice 17: auto-sprint;
ifmatches("%@&mina_mexicana[17]%","^(true|false)$",auto_sprint);
else;
	set(auto_sprint);
	set(@&mina_mexicana[17],"%auto_sprint%");
endif;


// Índice 18: labels;
replace(@&mina_mexicana[18],"--","-");
ifmatches("%@&mina_mexicana[18]%","^-*(?:\w+-*?)+-*$",&labels);
else;
	set(&labels,"horario-pvp-anoitecer-efeitos-quebrados-hotbar");
	set(@&mina_mexicana[18],"%&labels%");
endif;
replace(&labels,"--","-");

unset(&ids_para_quebrar_lista[]);
set(&ids_para_quebrar_temp,"%&ids_para_quebrar%");
set(&nomes_para_quebrar,"-->");
do();
	ifmatches("%&ids_para_quebrar_temp%","^\-*(.+?)\-*$",&ids_para_quebrar_temp,1);
	endif;
	ifmatches("%&ids_para_quebrar_temp%","^(.+?)\-(.+)$");
		match("%&ids_para_quebrar_temp%","^(.+?)\-(.+)$",{&id,&ids_para_quebrar_temp});
		push(&ids_para_quebrar_lista[],%&id%);

		getiteminfo(%&id%,&nome);
		set(&nomes_para_quebrar,"%&nomes_para_quebrar%&8, &f%&nome%");
	else;
		push(&ids_para_quebrar_lista[],%&ids_para_quebrar_temp%);

		getiteminfo(%&ids_para_quebrar_temp%,&nome);
		set(&nomes_para_quebrar,"%&nomes_para_quebrar%&8, &f%&nome%");
		break;
	endif;
	ifmatches("%&nomes_para_quebrar%","-->\S+ (.+)$",&nomes_para_quebrar,1);
	endif;
loop;

replace(&nomes_para_quebrar,"Minério de Esmeralda","&aMinério de Esmeralda&f");
replace(&nomes_para_quebrar,"Minério de Diamante","&bMinério de Diamante&f");
replace(&nomes_para_quebrar,"Minério de Carvão","&7Minério de Carvão&f");
replace(&nomes_para_quebrar,"Minério de Ouro","&eMinério de Ouro&f");
replace(&nomes_para_quebrar,"Madeira","&6Madeira&f");
replace(&nomes_para_quebrar,"Minério de Quartzo","&7Minério de Quartzo&f");
replace(&nomes_para_quebrar,"-->, ");
getiteminfo(%&id_ferramenta%,&nome_ferramenta);

if(!CTRL);
	log(" ");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &f&lCONFIGURAÇÕES");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fFerramenta: &b%&nome_ferramenta%");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fDurabilidade: &e%#durabilidade%");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fBlocos a quebrar: %&nomes_para_quebrar%");
	if(reparar_com_comando);
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fReparar com &e%&comando_reparar%&f: &aSIM!");
	else;
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fReparar&f: &cNAO!");
	endif;
	if(fugir_do_pvp);
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fFugir do &6PvP&f: &aSIM!");
	else;
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fFugir do &6PvP&f: &cNAO!");
	endif;
	if(auto_pvp_off);
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &6/pvp off &fao iniciar: &aSIM!");
	else;
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &6/pvp off &fao iniciar: &cNAO!");
	endif;
	if(tomar_pot);
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fBeber &bpot de velocidade&f: &aSIM!");
	else;
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fBeber &bpot de velocidade&f: &cNAO!");
	endif;
	if(auto_sprint);
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &bCorrer&f: &aSIM!");
	else;
		log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &bCorrer&f: &cNAO!");
	endif;
	do(20);
		wait(1t);
	until(CTRL);
endif;

ifcontains("%&labels%","horario");
	set(mostrar_label_horario);
endif;
ifcontains("%&labels%","pvp");
	set(mostrar_label_pvp);
endif;
ifcontains("%&labels%","anoitecer");
	set(mostrar_label_anoitecer);
endif;
ifcontains("%&labels%","efeitos");
	set(mostrar_label_efeitos);
endif;
ifcontains("%&labels%","quebrados");
	set(mostrar_label_quebrados);
endif;
ifcontains("%&labels%","hotbar");
	set(mostrar_label_hotbar);
endif;

if(CTRL);
	unset(&cores[]);
	push(&cores[],"§a");
	push(&cores[],"§b");
	push(&cores[],"§c");
	push(&cores[],"§d");
	push(&cores[],"§e");
	push(&cores[],"§1");
	push(&cores[],"§2");
	push(&cores[],"§3");
	push(&cores[],"§4");
	push(&cores[],"§5");
	push(&cores[],"§6");
	push(&cores[],"§9");
	arraysize(&cores[],#quantas_cores);

	
	do();		
		getiteminfo(%&id_ferramenta%,&nome_ferramenta);
		if(&nome_ferramenta == "None");
			set(&nome_ferramenta,"§5%&id_ferramenta%");
		endif;
		set(&prompt_ferramenta,"§f%&nome_ferramenta%");
		ifmatches("%&nome_ferramenta%","Diamante|Diamond");
			set(&prompt_ferramenta,"§b%&nome_ferramenta%");
		else;
			ifmatches("%&nome_ferramenta%","Stone|Pedra");
				set(&prompt_ferramenta,"§7%&nome_ferramenta%");
			endif;
		endif;
		set(&prompt_durabilidade,"§a%#durabilidade%");
		if(#durabilidade < 10);
			set(&prompt_durabilidade,"§4%#durabilidade%");
		elseif(#durabilidade < 20);
			set(&prompt_durabilidade,"§c%#durabilidade%");
		elseif(#durabilidade < 30);
			set(&prompt_durabilidade,"§e%#durabilidade%");
		elseif(#durabilidade < 40);
			set(&prompt_durabilidade,"§f%#durabilidade%");
		elseif(#durabilidade < 49);
			set(&prompt_durabilidade,"§a%#durabilidade%");
		else;
			set(&prompt_durabilidade,"§2%#durabilidade%");
		endif;

		set(&prompt_reparar,"§cNAO!");
		if(reparar_com_comando);
			set(&prompt_reparar,"§aSIM!");
		endif;
		set(&prompt_avisar_hotbar,"§cNAO!");
		if(avisar_hotbar_cheia);
			set(&prompt_avisar_hotbar,"§aSIM!");
		endif;
		set(&prompt_avisar_pvp,"§cNAO!");
		if(avisar_pvp);
			set(&prompt_avisar_pvp,"§aSIM!");
		endif;
		set(&prompt_fugir_do_pvp,"§cNAO!");
		if(fugir_do_pvp);
			set(&prompt_fugir_do_pvp,"§aSIM!");
		endif;

		set(&prompt_sprint,"§cNAO!");
		if(auto_sprint);
			set(&prompt_sprint,"§aSIM!");
		endif;
		set(&prompt_auto_pvp,"§cNAO!");
		if(auto_pvp_off);
			set(&prompt_auto_pvp,"§aSIM!");
		endif;
		set(&prompt_tomar_pot,"§cNAO!");
		if(tomar_pot);
			set(&prompt_tomar_pot,"§aSIM!");
		endif;
		set(&prompt_mostrar_quebrados_no_chat,"§cNAO!");
		if(mostrar_quebrados_no_chat);
			set(&prompt_mostrar_quebrados_no_chat,"§aSIM!");
		endif;
		set(&prompt_labels,"%&labels%");
		ifmatches("%&labels%","\w");
		else;
			set(&prompt_labels,"§cNENHUMA!");
		endif;

		unsafe(500);
			set(&ids_para_quebrar_temp,"%&ids_para_quebrar%");
			set(&nomes_para_quebrar,"-->");
			do();
				ifmatches("%&ids_para_quebrar_temp%","^\-*(.+?)\-*$",&ids_para_quebrar_temp,1);
				endif;
				ifmatches("%&ids_para_quebrar_temp%","^(.+?)\-(.+)$");
					match("%&ids_para_quebrar_temp%","^(.+?)\-(.+)$",{&id,&ids_para_quebrar_temp});

					getiteminfo(%&id%,&nome);
					set(&nomes_para_quebrar,"%&nomes_para_quebrar%§8 e §f%&nome%");
				else;
					getiteminfo(%&ids_para_quebrar_temp%,&nome);
					set(&nomes_para_quebrar,"%&nomes_para_quebrar%§8 e §f%&nome%");
					break;
				endif;
			loop;
			replace(&nomes_para_quebrar,"Minério de Esmeralda","§aMinério de Esmeralda§f");
			replace(&nomes_para_quebrar,"Minério de Diamante","§bMinério de Diamante§f");
			replace(&nomes_para_quebrar,"Minério de Carvão","§7Minério de Carvão§f");
			replace(&nomes_para_quebrar,"Minério de Ouro","§eMinério de Ouro§f");
			replace(&nomes_para_quebrar,"Madeira","§6Madeira§f");
			replace(&nomes_para_quebrar,"Minério de Quartzo","§7Minério de Quartzo§f");
			replace(&nomes_para_quebrar,"-->§8 e ");
			
			set(&blocos_quebrados_temp,"%&blocos_quebrados%");
			set(&nomes_quebrados,"-->");
			do();
				ifmatches("%&blocos_quebrados_temp%","^[- ]*(.+?)[- ]*$",&blocos_quebrados_temp,1);
				endif;
				ifmatches("%&blocos_quebrados_temp%","^\s*(\S+)\-(\d+) *(.+)$");
					match("%&blocos_quebrados_temp%","^\s*(\S+)\-(\d+) *(.+)$",{&id,#quantidade,&blocos_quebrados_temp});
					if(&id != "0");
						getiteminfo(%&id%,&nome);
						set(&nomes_quebrados,"%&nomes_quebrados%§8 e §f%&nome% §8(§6%#quantidade%§8)");
					endif;
				else;
					ifmatches("%&blocos_quebrados_temp%","^\s*(\S+)\-(\d+)");
						match("%&blocos_quebrados_temp%","^\s*(\S+)\-(\d+)",{&id,#quantidade});
						if(&id != "0");
							getiteminfo(%&blocos_quebrados_temp%,&nome);
							set(&nomes_quebrados,"%&nomes_quebrados%§8 e §f%&nome% §8(§6%#quantidade%§8)");
						endif;
					endif;
					if(&nomes_quebrados == "-->");
						set(&nomes_quebrados,"§7§onada ainda...§r");
					endif;
					break;
				endif;
			loop;
			ifcontains("%&nomes_quebrados%","nada ainda");
			else;
				set(&nomes_quebrados,"%&nomes_quebrados% §5--> §dSelecione para §d§nlimpar§r§5")
			endif;
			replace(&nomes_quebrados,"Minério de Esmeralda","§aMinério de Esmeralda§f");
			replace(&nomes_quebrados,"Minério de Diamante","§bMinério de Diamante§f");
			replace(&nomes_quebrados,"Minério de Carvão","§7Minério de Carvão§f");
			replace(&nomes_quebrados,"Minério de Ouro","§eMinério de Ouro§f");
			replace(&nomes_quebrados,"Madeira","§6Madeira§f");
			replace(&nomes_quebrados,"Minério de Quartzo","§7Minério de Quartzo§f");
			replace(&nomes_quebrados,"-->§8 e ");
		endunsafe;
		

		set(&prompt,"§f§lMINA §2§lMEX§f§lIC§4§lANA");
		set(&prompt,"%&prompt%,");
		set(&prompt,"%&prompt%,§fQuebrar: %&nomes_para_quebrar%");
		set(&prompt,"%&prompt%,§6Quebrados: %&nomes_quebrados%");
		set(&prompt,"%&prompt%,§fFerramenta: %&prompt_ferramenta%");
		set(&prompt,"%&prompt%,§eDurabilidade: %&prompt_durabilidade%");
		set(&prompt,"%&prompt%,§fReparar: %&prompt_reparar%");
		if(reparar_com_comando);
			set(&prompt,"%&prompt%,  §7comando rep: §f%&comando_reparar%");
		endif;
		set(&prompt,"%&prompt%,§bSprint: %&prompt_sprint%");
		set(&prompt,"%&prompt%,§fAvisar §cPvP: %&prompt_avisar_pvp%");
		if(avisar_pvp);
			set(&prompt,"%&prompt%,§f  §7tempo aviso: §f%#aviso_segundos%§7s");
		endif;
		set(&prompt,"%&prompt%,§fAvisar §6hotbar: %&prompt_avisar_hotbar%");
		set(&prompt,"%&prompt%,§fBeber §bvelo§f: %&prompt_tomar_pot%");
		
		set(&prompt,"%&prompt%,§aFugir§f do §cPvP§f: %&prompt_fugir_do_pvp%");
		if(fugir_do_pvp);
			set(&prompt,"%&prompt%,§f  §7tempo fuga: §f%#fugir_segundos%§7s");
		endif;
		if(fugir_do_pvp);
			set(&prompt,"%&prompt%,  §7comando fuga: §f%&comando_de_fuga%");
		endif;
		set(&prompt,"%&prompt%,§cPvP OFF §fao iniciar: %&prompt_auto_pvp%");
		set(&prompt,"%&prompt%,§eQuebrados §fno §echat§f: %&prompt_mostrar_quebrados_no_chat%");
		set(&prompt,"%&prompt%,§eLabels: §f%&prompt_labels%");
		

		set(&prompt,"%&prompt%,");
		set(&prompt,"%&prompt%,§2-> §aContinuar §8(§7ESC§8)");
		set(&prompt,"%&prompt%,§5-> §dResetar");
		set(&prompt,"%&prompt%,§4<- §cSair");

		unset(&resposta);
		prompt(&resposta,"$$[ [%&prompt%]]","§fEscolha uma §6opção §fda lista!",true,"");
		strip(&resposta,"%&resposta%");

		if(&resposta == "-> Resetar");
			unset(&resposta);

			if(versao_152);
				set(&ids_para_quebrar,"129-56-16");
				set(&id_ferramenta,"278");
			else;
				set(&ids_para_quebrar,"emerald_ore-diamond_ore-coal_ore");
				set(&id_ferramenta,"diamond_pickaxe");
			endif;
			set(#durabilidade,150);
			unset(reparar_com_comando);
			set(&comando_reparar,"/reparar");
			set(avisar_hotbar_cheia);
			set(avisar_pvp);
			set(fugir_do_pvp);
			set(&comando_de_fuga,"/minerar");
			set(&blocos_quebrados," 0-0");
			unset(auto_pvp_off);
			unset(mostrar_quebrados_no_chat);
			set(#fugir_segundos,15);
			set(#aviso_segundos,30);
			set(#intervalo_aviso_segundos,2);
			set(tomar_pot);
			set(auto_sprint);
			set(&labels,"horario-pvp-anoitecer-efeitos-quebrados-hotbar");

			log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &aMenu resetado!");
			wait(1);
		elseif((!&resposta)||(&resposta == "-> Continuar (ESC)"));
			break;
		elseif(&resposta == "<- Sair");
			unset(@&mina_mexicana[0]);
			setlabel("horario","");
			setlabel("efeitos","");
			setlabel("rendimento","");
			log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &f&lMINA &2&lMEX&f&lIC&4&lANA&f&l: &c&lOFF&4&l!");
			stop("MINA_MEXICANA");
			stop();
		endif;
		ifmatches("%&resposta%","^Quebrar:");
			do();
				unset(&ids_para_quebrar_lista[]);
				set(&ids_para_quebrar_temp,"%&ids_para_quebrar%");
				set(&prompt_para_quebrar,"§2-> §aAdicionar bloco,§8<- §7Voltar §8(§7ESC§8), ");
				unset(#cor_agora);
				unsafe;
					do();
						ifmatches("%&ids_para_quebrar_temp%","^\-*(.+?)\-*$",&ids_para_quebrar_temp,1);
						endif;
						ifmatches("%&ids_para_quebrar_temp%","^(.+?)\-(.+)$");
							match("%&ids_para_quebrar_temp%","^(.+?)\-(.+)$",{&id,&ids_para_quebrar_temp});
							

							push(&ids_para_quebrar_lista[],"%&id%");

							getiteminfo(%&id%,&nome);
							set(&prompt_para_quebrar,"%&prompt_para_quebrar%,%&cores[%#cor_agora%]%%&nome% §8(§7id %&cores[%#cor_agora%]%%&id%§8)");
						else;
							push(&ids_para_quebrar_lista[],%&ids_para_quebrar_temp%);

							getiteminfo(%&ids_para_quebrar_temp%,&nome);
							set(&prompt_para_quebrar,"%&prompt_para_quebrar%,%&cores[%#cor_agora%]%%&nome% §8(§7id %&cores[%#cor_agora%]%%&ids_para_quebrar_temp%§8)");
							break;
						endif;
						ifmatches("%&prompt_para_quebrar%","-->\S+ (.+)$",&prompt_para_quebrar,1);
						endif;
						inc(#cor_agora);
						if(#cor_agora > (#quantas_cores - 1));
							#cor_agora = 0;
						endif;
					loop;
				endunsafe;
				unset(&resposta2);
				prompt(&resposta2,"$$[ [%&prompt_para_quebrar%]]","§fEscolha um §6bloco §fpara remove-lo, ou adicione novos blocos para a macro quebrar!");
				strip(&resposta2,"%&resposta2%");
				if((&resposta2 == "<- Voltar (ESC)")||(!&resposta2));
					break;
				elseif(&resposta2 == "-> Adicionar bloco");
					unset(&resposta2);
					prompt(&resposta2,"$$i:d","§fEscolha um §6bloco §fpara adicionar à lista de blocos que a macro pode quebrar!");
					replace(&resposta2,":0");
					ifmatches("%&resposta2%","^\w+(?::\d+)?$");
						if(&resposta2 != "air")&&(&resposta2 != "0");
							indexof(&ids_para_quebrar_lista[],#index,"%&resposta2%");
							if(#index != -1);
								log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cEsse bloco já está na lista!");
								wait(1);
							else;
								set(&ids_para_quebrar,"%&ids_para_quebrar%-%&resposta2%");
								log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &aBloco adicionado!");
							endif;
						else;
							log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cResposta inválida. &8[1]");
							wait(1);
						endif;
					else;
						log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cResposta inválida. &8[2]");
					endif;
				else;
					ifmatches("%&resposta2%","\(id (\w+(?::\d+)?)\)",&id,1);
						arraysize(&ids_para_quebrar_lista[],#size);
						if(#size == 1);
							log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cA lista de blocos não pode ficar vazia!");
							wait(1);
						else;
							set(&ids_para_quebrar,"-%&ids_para_quebrar%-");
							replace(&ids_para_quebrar,"-%&id%-");
							replace(&ids_para_quebrar,"--","-");
							replace(&ids_para_quebrar,"--","-");
							ifmatches("%&ids_para_quebrar%","^-*(.+?)-*$",&ids_para_quebrar,1);
							endif;
						endif;
					endif;
				endif;
			loop;


			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Quebrados:");
			set(&blocos_quebrados," 0-0");
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Ferramenta:");
			unset(&resposta2);
			prompt(&resposta2,"$$[ [§fPicareta de §bdiamante,§eMachado de §bdiamante,§6Pa de §bdiamante,§fPicareta de ferro,§eMachado §fde ferro,§6Pa §fde ferro,§fPicareta de §7pedra,§eMachado de §7pedra,§6Pa de §7pedra,,§5Outro]]","§fEscolha uma §6opção §fda lista!");
			strip(&resposta2,"%&resposta2%");
			if((&resposta2 == "Picareta de diamante")&&(versao_152));
				set(&id_ferramenta,"278");
			elseif((&resposta2 == "Picareta de diamante")&&(!versao_152));
				set(&id_ferramenta,"diamond_pickaxe");
			elseif((&resposta2 == "Machado de diamante")&&(versao_152));
				set(&id_ferramenta,"279");
			elseif((&resposta2 == "Machado de diamante")&&(!versao_152));
				set(&id_ferramenta,"diamond_axe");
			elseif((&resposta2 == "Pa de diamante")&&(versao_152));
				set(&id_ferramenta,"277");
			elseif((&resposta2 == "Pa de diamante")&&(!versao_152));
				set(&id_ferramenta,"diamond_shovel");

			elseif((&resposta2 == "Picareta de ferro")&&(versao_152));
				set(&id_ferramenta,"257");
			elseif((&resposta2 == "Picareta de ferro")&&(!versao_152));
				set(&id_ferramenta,"iron_pickaxe");
			elseif((&resposta2 == "Machado de ferro")&&(versao_152));
				set(&id_ferramenta,"258");
			elseif((&resposta2 == "Machado de ferro")&&(!versao_152));
				set(&id_ferramenta,"iron_axe");
			elseif((&resposta2 == "Pa de ferro")&&(versao_152));
				set(&id_ferramenta,"256");
			elseif((&resposta2 == "Pa de ferro")&&(!versao_152));
				set(&id_ferramenta,"iron_shovel");

			elseif((&resposta2 == "Picareta de pedra")&&(versao_152));
				set(&id_ferramenta,"274");
			elseif((&resposta2 == "Picareta de pedra")&&(!versao_152));
				set(&id_ferramenta,"stone_pickaxe");
			elseif((&resposta2 == "Machado de pedra")&&(versao_152));
				set(&id_ferramenta,"275");
			elseif((&resposta2 == "Machado de pedra")&&(!versao_152));
				set(&id_ferramenta,"stone_axe");
			elseif((&resposta2 == "Pa de pedra")&&(versao_152));
				set(&id_ferramenta,"273");
			elseif((&resposta2 == "Pa de pedra")&&(!versao_152));
				set(&id_ferramenta,"stone_shovel");
			
			elseif(&resposta2 == "Outro");
				unset(&resposta2);
				prompt(&resposta2,"$$i","§fEscolha a §6ferramenta §fque você quer utilizar!");
				ifmatches("%&resposta2%","^\w+(?::\d+)?$");
					set(&id_ferramenta,"%&resposta2%");
				endif;
			endif;
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Durabilidade:");
			unset(&resposta2);
			prompt(&resposta2,"$$?","Digite a §6durabilidade limite§f que a ferramenta pode atingir! §8Min: §610");
			ifmatches("%&resposta2%","^\d+$",#durabilidade_temp);
				if(#durabilidade_temp > 10);
					#durabilidade = #durabilidade_temp;
				else;
					log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cA durabilidade mínima é 10.");
					wait(1);
				endif;
			elseif(&resposta2);
				log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cResposta inválida. Exemplo válido: §650");
			endif;
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Reparar:");
			toggle(reparar_com_comando);
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^  comando rep:");
			unset(&resposta2);
			prompt(&resposta2,"$$?","§fDigite o §acomando§f de §areparar§f, iniciado por §ebarra§f. §8Exemplo válido: §6/reparar");
			ifmatches("%&resposta2%","^\/.+$",&comando_reparar);
			endif;
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Sprint:");
			toggle(auto_sprint);
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Avisar PvP:");
			toggle(avisar_pvp);
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^  tempo aviso:");
			unset(&resposta2);
			prompt(&resposta2,"$$?","Digite §aquantos segundos §fantes de o §cPvP iniciar §fvocê quer ser §aavisado! §8Ex. válido: §630");
			ifmatches("%&resposta2%","^(\d+)(?:s|segundos)?$",#aviso_segundos_temp,1);
				if(#aviso_segundos_temp >= 1);
					#aviso_segundos = #aviso_segundos_temp;
				else;
					log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cResposta inválida. Exemplo válido: §630");
					wait(1);
				endif;
			elseif(&resposta2);
				log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cResposta inválida. Exemplo válido: §630");
			endif;
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Avisar hotbar:");
			toggle(avisar_hotbar_cheia);
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Beber velo:");
			toggle(tomar_pot);
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Fugir do PvP:");
			toggle(fugir_do_pvp);
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^  tempo fuga:");
			unset(&resposta2);
			prompt(&resposta2,"$$?","Digite §aquantos segundos §fantes de o §cPvP iniciar §fvocê quer §6FUGIR! §8Ex. válido: §615");
			ifmatches("%&resposta2%","^(\d+)(?:s|segundos)?$",#fugir_segundos_temp,1);
				if(#fugir_segundos_temp >= 1);
					#fugir_segundos = #fugir_segundos_temp;
				else;
					log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cResposta inválida. Exemplo válido: §615");
					wait(1);
				endif;
			elseif(&resposta2);
				log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cResposta inválida. Exemplo válido: §615");
			endif;

			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^  comando fuga:");
			unset(&resposta2);
			prompt(&resposta2,"$$?","§fDigite o §acomando§f para §afugir do §cPvP§f, iniciado por §ebarra§f. §8Exemplo válido: §6/minerar§8, §6/spawn");
			ifmatches("%&resposta2%","^\/.+$",&comando_de_fuga);
			endif;
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^PvP OFF ao iniciar:");
			toggle(auto_pvp_off);
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Quebrados no chat:");
			toggle(mostrar_quebrados_no_chat);
			unset(&resposta);
		endif;
		ifmatches("%&resposta%","^Labels:");
			do();
				set(&lista_labels,"§6§fLista de §a§lLABELS");
				set(&lista_labels,"%&lista_labels%,");
				ifcontains("%&labels%","horario");
					set(&lista_labels,"%&lista_labels%,§6Horário§f: §aSIM!");
				else;
					set(&lista_labels,"%&lista_labels%,§6Horário§f: §cNÃO!");
				endif;
				ifcontains("%&labels%","anoitecer");
					set(&lista_labels,"%&lista_labels%,§fAviso de §cPvP§f: §aSIM!");
				else;
					set(&lista_labels,"%&lista_labels%,§fAviso de §cPvP§f: §cNÃO!");
				endif;
				ifcontains("%&labels%","pvp");
					set(&lista_labels,"%&lista_labels%,§cPvP §fStatus: §aSIM!");
				else;
					set(&lista_labels,"%&lista_labels%,§cPvP §fStatus: §cNÃO!");
				endif;
				ifcontains("%&labels%","efeitos");
					set(&lista_labels,"%&lista_labels%,§dEfeitos§f: §aSIM!");
				else;
					set(&lista_labels,"%&lista_labels%,§dEfeitos§f: §cNÃO!");
				endif;
				ifcontains("%&labels%","quebrados");
					set(&lista_labels,"%&lista_labels%,§fBlocos §6quebrados: §aSIM!");
				else;
					set(&lista_labels,"%&lista_labels%,§fBlocos §6quebrados: §cNÃO!");
				endif;
				ifcontains("%&labels%","hotbar");
					set(&lista_labels,"%&lista_labels%,§eHotbar: §aSIM!");
				else;
					set(&lista_labels,"%&lista_labels%,§6Hotbar: §cNÃO!");
				endif;
				unset(&resposta2);
				prompt(&resposta2,"$$[ [%&lista_labels%, ,§2<- §aVoltar §8(§7ESC§8)]]","§fEscolha uma §6opção §fda lista!");
				strip(&resposta2,"%&resposta2%");
				if((!&resposta2)||(&resposta2 == "<- Voltar (ESC)"));
					break;
				endif;
				ifcontains("%&resposta2%","Horário");
					toggle(mostrar_label_horario);
					if(!mostrar_label_horario);
						replace(&labels,"horario");
						replace(&labels,"--","-");
					else;
						set(&labels,"%&labels%-horario");
					endif;
				endif;
				ifcontains("%&resposta2%","PvP Status");
					toggle(mostrar_label_pvp);
					if(!mostrar_label_pvp);
						replace(&labels,"pvp");
						replace(&labels,"--","-");
					else;
						set(&labels,"%&labels%-pvp");
					endif;
				endif;
				ifcontains("%&resposta2%","Aviso de PvP");
					toggle(mostrar_label_anoitecer);
					if(!mostrar_label_anoitecer);
						replace(&labels,"anoitecer");
						replace(&labels,"--","-");
					else;
						set(&labels,"%&labels%-anoitecer");
					endif;
				endif;
				ifcontains("%&resposta2%","Efeitos");
					toggle(mostrar_label_efeitos);
					if(!mostrar_label_efeitos);
						replace(&labels,"efeitos");
						replace(&labels,"--","-");
					else;
						set(&labels,"%&labels%-efeitos");
					endif;
				endif;
				ifcontains("%&resposta2%","Blocos quebrados");
					toggle(mostrar_label_quebrados);
					if(!mostrar_label_quebrados);
						replace(&labels,"quebrados");
						replace(&labels,"--","-");
					else;
						set(&labels,"%&labels%-quebrados");
					endif;
				endif;
				ifcontains("%&resposta2%","Hotbar");
					toggle(mostrar_label_hotbar);
					if(!mostrar_label_hotbar);
						replace(&labels,"hotbar");
						replace(&labels,"--","-");
					else;
						set(&labels,"%&labels%-hotbar");
					endif;
				endif;
				replace(&labels,"--","-");
				replace(&labels,"--","-");
			loop;
			ifmatches("%&labels%","^-*(.+?)-*$",&labels,1);
			endif;
			if(!&labels);
				set(&labels,"-");
			endif;

			unset(&resposta);
		endif;

		set(@&mina_mexicana[1],"%&ids_para_quebrar%");
		set(@&mina_mexicana[2],"%&id_ferramenta%");
		set(@&mina_mexicana[3],"%#durabilidade%");
		set(@&mina_mexicana[4],"%reparar_com_comando%");
		set(@&mina_mexicana[5],"%avisar_hotbar_cheia%");
		set(@&mina_mexicana[6],"%avisar_pvp%");
		set(@&mina_mexicana[7],"%&comando_reparar%");
		set(@&mina_mexicana[8],"%fugir_do_pvp%");
		set(@&mina_mexicana[9],"%&comando_de_fuga%");
		set(@&mina_mexicana[10],"%&blocos_quebrados%");
		set(@&mina_mexicana[11],"%auto_pvp_off%");
		set(@&mina_mexicana[12],"%mostrar_quebrados_no_chat%");
		set(@&mina_mexicana[13],"%#fugir_segundos%");
		set(@&mina_mexicana[14],"%#aviso_segundos%");
		set(@&mina_mexicana[15],"%#intervalo_aviso_segundos%");
		set(@&mina_mexicana[16],"%tomar_pot%");
		set(@&mina_mexicana[17],"%auto_sprint%");
		set(@&mina_mexicana[18],"%&labels%");
	loop;
endif;


if(auto_pvp_off);
	log(" ");
	log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &f/pvp off");
	echo("/pvp off");
endif;

unset(mostrar_label_horario);
unset(mostrar_label_pvp);
unset(mostrar_label_anoitecer);
unset(mostrar_label_efeitos);
unset(mostrar_label_quebrados);
unset(mostrar_label_hotbar);

ifcontains("%&labels%","horario");
	set(mostrar_label_horario);
endif;
ifcontains("%&labels%","pvp");
	set(mostrar_label_pvp);
endif;
ifcontains("%&labels%","anoitecer");
	set(mostrar_label_anoitecer);
endif;
ifcontains("%&labels%","efeitos");
	set(mostrar_label_efeitos);
endif;
ifcontains("%&labels%","quebrados");
	set(mostrar_label_quebrados);
endif;
ifcontains("%&labels%","hotbar");
	set(mostrar_label_hotbar);
endif;


ifmatches("%&ids_para_quebrar%","^-*(.+?)-*$",&ids_para_quebrar);
endif;
replace(&ids_para_quebrar,"-","|");

unset(ler_bloco);
unset(#x_quebrando);
unset(#y_quebrando);
unset(#z_quebrando);
unset(#x_quebrando_anterior);
unset(#y_quebrando_anterior);
unset(#z_quebrando_anterior);
unset(&ultimo_quebrado);

// Segundo no qual anoitece;
#timestamp_noite = (#hora_noite * 60) + (#minuto_noite);

// Segundo no qual amanhece;
#timestamp_dia = (#hora_dia * 60) + (#minuto_dia);

// Timestamp que deve começar a avisar sobre o PvP;
#timestamp_iniciar_aviso = #timestamp_noite - (#aviso_segundos);

// Timestamp que deve fugir;
#timestamp_fugir = #timestamp_noite - (#fugir_segundos);

unset(&pots[]);
push(&pots[],"373:8194");
push(&pots[],"373:8226");
push(&pots[],"373:8258");
push(&pots[],"373:8290");

unset(&label_quebrados_atual);

unset(#timestamp_ultimo_aviso);
unsafe(1000);
	do();
		unset(&label_horario);
		unset(&label_pot);
		unset(&label_quebrados_e_hotbar);

		match("%DAYTIME%","^(\d+):(\d+)$",{#hora,#minuto});
		#timestamp_agora = (#hora * 60) + #minuto;

		if(mostrar_label_horario);
			set(&label_horario_1,"&6%DAYTIME%");
		endif;
		if(mostrar_label_pvp);
			if((#timestamp_agora >= #timestamp_noite)||(#timestamp_agora <= #timestamp_dia));
				set(&label_horario_2,"&cPvP ativado");
			else;
				set(&label_horario_2,"&aPvP desativado");
			endif;
		endif;
		if((&label_horario_1)||(&label_horario_2));
			if((&label_horario_1)&&(&label_horario_2));
				set(&label_horario,"%&label_horario_1%\n%&label_horario_2%");
			elseif(&label_horario_1);
				set(&label_horario,"%&label_horario_1%");
			else;
				set(&label_horario,"%&label_horario_2%");
			endif;
		endif;
		if(mostrar_label_anoitecer);
			if((#timestamp_agora >= #timestamp_noite)||(#timestamp_agora <= #timestamp_dia));
			else;
				#segundos_ate_pvp = #timestamp_noite - #timestamp_agora;
				if(#segundos_ate_pvp > 60);
				else;
					if(#segundos_ate_pvp > 30);
						set(&cor,"a");
					elseif(#segundos_ate_pvp > 20);
						set(&cor,"e");
					elseif(#segundos_ate_pvp > 10);
						set(&cor,"c");
					else;
						set(&cor,"4");
					endif;
					set(&label_horario,"%&label_horario%\n&%&cor%%#segundos_ate_pvp% segundos até anoitecer!");
				endif;
			endif;
		endif;

		if( ((#timestamp_agora - #timestamp_ultimo_ataque) > 5)||((#timestamp_agora - #timestamp_ultimo_ataque) < 0));
			if(ler_bloco);
				unset(ler_bloco);
				#timestamp_ultimo_ataque = #timestamp_agora;
			endif;
		endif;
		if((fugir_do_pvp)||(avisar_pvp));
			if(avisar_pvp);
				#segundos_ate_pvp = #timestamp_noite - #timestamp_agora;
				if((#timestamp_agora >= #timestamp_iniciar_aviso)&&(#timestamp_agora <= #timestamp_noite));
					if( (#timestamp_agora - #timestamp_ultimo_aviso) > #intervalo_aviso_segundos);
						if(#segundos_ate_pvp > 20);
							set(&cor,"a");
						elseif(#segundos_ate_pvp > 10);
							set(&cor,"e");
						elseif(#segundos_ate_pvp > 5);
							set(&cor,"c");
						else;
							set(&cor,"4");
						endif;
						log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cPvP &finiciando em &%&cor%%#segundos_ate_pvp% segundos&f...");
						#timestamp_ultimo_aviso = #timestamp_agora;
					endif;
				else;
					unset(#timestamp_ultimo_aviso);
				endif;
			endif;
			if(fugir_do_pvp);
				if((#timestamp_agora >= #timestamp_fugir)&&(#timestamp_agora <= #timestamp_noite));
					log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &fHora de fugir do PvP...");
					break;
				endif;
			endif;
		endif;

		ifmatches("<%HITID%><%HITID%:%HITDATA%","<(%&ids_para_quebrar%)>");
			if((#x_quebrando != #x_quebrando_anterior)||(#y_quebrando != #y_quebrando_anterior)||(#z_quebrando != #z_quebrando_anterior));
				#x_quebrando_anterior = #x_quebrando;
				#y_quebrando_anterior = #y_quebrando;
				#z_quebrando_anterior = #z_quebrando;
				#timestamp_ultimo_anterior = #timestamp_agora;
			endif;
			#x_quebrando = HITX;
			#y_quebrando = HITY;
			#z_quebrando = HITZ;
			getid(%#x_quebrando%,%#y_quebrando%,%#z_quebrando%,&id,#data);
			ifmatches("<%&id%><%&id%:%#data%>","<(%&ids_para_quebrar%)>");
				set(&id_ultimo_quebrado,"%HITID%");
				set(#data_ultimo_quebrado,%HITDATA%);

				set(ler_bloco);

				ifmatches(%ITEM%,"^(%&id_ferramenta%)$");
					if(DURABILITY > #durabilidade);
						key(attack);
						#timestamp_ultimo_ataque = #timestamp_agora;
					elseif(reparar_com_comando);
						log(" ");
						echo("%&comando_reparar%");
						log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &f%&comando_reparar%");
						wait(10t);
					else;
						log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cDurabilidade baixa!");
						wait(20t);
					endif;
				else;
					pick(%&id_ferramenta%);
					wait(1t);
				endif;
			endif;
		else;
			wait(1t);
		endif;
		if(ler_bloco);	
			getid(%#x_quebrando%,%#y_quebrando%,%#z_quebrando%,&id);
			if(&id != &id_ultimo_quebrado);
			else;
				if((#timestamp_agora - #timestamp_ultimo_anterior) < 2);
					if(#y_quebrando_anterior != 0);
						getid(%#x_quebrando_anterior%,%#y_quebrando_anterior%,%#z_quebrando_anterior%,&id);
					endif;
				endif;
			endif;
			if((&id != &id_ultimo_quebrado)&&(&id != "1")&&(&id != "4")&&(&id != "stone")&&(&id != "cobblestone"));
				unset(#x_quebrando);
				unset(#y_quebrando);
				unset(#z_quebrando);

				unset(ler_bloco);
				#contagem_anterior = -1;

				ifmatches("%&blocos_quebrados%"," %&id_ultimo_quebrado%:%#data_ultimo_quebrado%-(\d+)",#contagem_anterior,1);
				else;
					ifmatches("%&blocos_quebrados%"," %&id_ultimo_quebrado%-(\d+)",#contagem_anterior,1);
					endif;
				endif;
				#contagem_atual = #contagem_anterior + 1;
				if(#contagem_anterior == -1);
					inc(#contagem_atual);
					set(&blocos_quebrados,"%&blocos_quebrados% %&id_ultimo_quebrado%:%#data_ultimo_quebrado%-1");
				else;
					replace(&blocos_quebrados," %&id_ultimo_quebrado%-%#contagem_anterior%"," %&id_ultimo_quebrado%-%#contagem_atual%");
					replace(&blocos_quebrados," %&id_ultimo_quebrado%:%#data_ultimo_quebrado%-%#contagem_anterior%"," %&id_ultimo_quebrado%:%#data_ultimo_quebrado%-%#contagem_atual%");
					set(@&mina_mexicana[10],"%&blocos_quebrados%");
				endif;
				if((mostrar_quebrados_no_chat)||(mostrar_label_quebrados));
					getiteminfo("%&id_ultimo_quebrado%:%#data_ultimo_quebrado%",&nome);
					unset(&cor);
					ifmatches("%&nome%","Esmeralda|Emerald");
						set(&cor,"a");
					endif;
					ifmatches("%&nome%","Diamante|Diamond");
						set(&cor,"b");
					endif;
					ifmatches("%&nome%","Ferro|Iron|Carv.o|Coal");
						set(&cor,"7");
					endif;
					ifmatches("%&nome%","Quartz");
						set(&cor,"c");
					endif;
					ifmatches("%&nome%","Ouro|Gold");
						set(&cor,"e");
					endif;
					ifmatches("%&nome%","Lazuli|L.pis");
						set(&cor,"1");
					endif;
					if(!&cor);
						set(&cor,"e");
					endif;
					if(mostrar_quebrados_no_chat);
						log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &%&cor%%&nome% &fquebrados: &%&cor%%#contagem_atual%");
					endif;
					if(mostrar_label_quebrados);
						set(&label_quebrados_atual,"%&label_quebrados_atual%\n&%&cor%&l%&nome%&f: &%&cor%&l%#contagem_atual% ");
						replace(&label_quebrados_atual,"\n&%&cor%&l%&nome%&f: &%&cor%&l%#contagem_anterior% ","");
					endif;
				endif;
			endif;
		endif;
		if(((tomar_pot)||(mostrar_label_efeitos))&&(GUI == "NONE"));
			set(efeito_ok);
			if(tomar_pot);
				unset(efeito_ok);
			endif;
			foreach("effects");
				if(EFFECTID != 16);
					if(mostrar_label_efeitos);
						if(EFFECTTIME > 10);
							set(&cor,"a");
						elseif(EFFECTTIME > 7);
							set(&cor,"e");
						elseif(EFFECTTIME > 4);
							set(&cor,"f");
						else;
							set(&cor,"c");
						endif;
						set(&cor_efeito,"f");
						ifmatches("%EFFECT%","velocidade");
							set(&cor_efeito,"b");
						endif;
						ifmatches("%EFFECT%","regen");
							set(&cor_efeito,"d");
						endif;
						set(&label_pot,"%&label_pot%\n&8[&%&cor%%EFFECTTIME%&8] &%&cor_efeito%%EFFECT%");
					endif;
					if((EFFECTID == 1)&&(EFFECTTIME > 4));
						set(efeito_ok);
						if(!mostrar_label_efeitos);
							break;
						endif;
					endif;
				endif;
			next;
			if(!efeito_ok);
				foreach(&pots[],&id);
					getslot(%&id%,#slot);
					if((#slot >= 1)&&(#slot <= 9));
						break;
					endif;
				next;
				if((#slot >= 1)&&(#slot <= 9));
					slot(%#slot%);
					#stack = STACKSIZE;
					#item = ITEM;
					wait(1t);
					match("%DAYTIME%","^(\d+):(\d+)$",{#hora,#minuto});
					#timestamp_backup = (#hora * 60) + #minuto;
					do();
						key(use);
						match("%DAYTIME%","^(\d+):(\d+)$",{#hora,#minuto});
						#timestamp_agora = (#hora * 60) + #minuto;
					while((#stack == STACKSIZE)&&(ITEM == #item)&&(GUI == "NONE")&&(INVSLOT == #slot)&&((#timestamp_agora - #timestamp_backup) < 5));
				endif;
			endif;
		endif;
		if( ((avisar_hotbar_cheia)||(mostrar_label_hotbar)) &&(GUI == "NONE"));
			unset(#livres_hotbar);
			for(#slot,1,9);
				if(versao_152);
					getslotitem(%#slot%,#id,#stack,#data);
					set(&id,"%#id%");
				else;
					getslotitem(%#slot%,&id,#stack,#data);
				endif;
				if(#stack == 0);
					inc(#livres_hotbar,64);
				else;
					indexof(&ids_para_quebrar_lista[],#index,"%&id%");
					if(#index == -1);
						indexof(&ids_para_quebrar_lista[],#index,"%&id%:%#data%");
					endif;
					if((#index > -1)&&(#stack < 64));
						#stack_livre = 64 - #stack;
						inc(#livres_hotbar,%#stack_livre%);
					endif;
				endif;
			next;
			if(#livres_hotbar == 0);
				if(mostrar_label_hotbar)
					set(&label_quebrados_e_hotbar,"&cHotbar cheia!\n%&label_quebrados_e_hotbar%");
				endif;
				match("%DAYTIME%","^(\d+):(\d+)$",{#hora,#minuto});
				#timestamp_agora = (#hora * 60) + #minuto;
				if((#timestamp_agora - #timestamp_ultimo_log_hotbar) > 10);
					#timestamp_ultimo_log_hotbar = (#hora * 60) + #minuto;
					if(avisar_hotbar_cheia);
						log(" ");
						log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &cHotbar cheia!");
					endif;
				endif;
			else;
				unset(#timestamp_ultimo_log_hotbar);
				if(mostrar_label_hotbar);
					if(#livres_hotbar >= 64);
						set(&cor,"a");
					elseif(#livres_hotbar > 32);
						set(&cor,"e");
					elseif(#livres_hotbar > 20);
						set(&cor,"f");
					else;
						set(&cor,"c");
					endif;
					set(&label_quebrados_e_hotbar,"&%&cor%%#livres_hotbar% &fespaços na hotbar\n%&label_quebrados_e_hotbar%");
				endif;
			endif;
		endif;
		if((auto_sprint)&&(KEY_W));
			sprint;
		elseif(auto_sprint);
			unsprint;
		endif;
		if(&label_horario);
			ifmatches("%&label_horario%","^(?:\\n)*(.+?)(?:\\n)*$",&label_horario,1);
			endif;
			setlabel("horario","%&label_horario%");
		endif;
		if(&label_pot);
			ifmatches("%&label_pot%","^(?:\\n)*(.+?)(?:\\n)*$",&label_pot,1);
			endif;
			set(&label_pot,"§5§l-- EFEITOS --\n%&label_pot%")
			replace(&label_pot,"\n\n","\n");
			replace(&label_pot,"\n\n","\n");
			setlabel("efeitos","%&label_pot%");
		elseif(mostrar_label_efeitos);
			setlabel("efeitos","&fSem poção ativa.");
		endif;
		if((&label_quebrados_e_hotbar)||(&label_quebrados_atual));	
			if(&label_quebrados_atual);
				set(&label_quebrados_e_hotbar,"%&label_quebrados_e_hotbar%\n%&label_quebrados_atual%")
			endif;
			ifmatches("%&label_quebrados_e_hotbar%","^(?:\\n)*(.+?)(?:\\n)*$",&label_quebrados_e_hotbar,1);
			endif;
			replace(&label_quebrados_e_hotbar,"\n\n","\n");
			replace(&label_quebrados_e_hotbar,"\n\n","\n");
			setlabel("rendimento","%&label_quebrados_e_hotbar%");
		endif;
	loop;
endunsafe;

echo("%&comando_de_fuga%");
log(" ");
log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &f%&comando_de_fuga%");
wait(1);
log(" ");
log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &aReinicie a macro quando voltar para a mina!");

unset(@&mina_mexicana[0]);
log( );
log("&f&l[&2&lMA&f&lC&4&lRO&f&l] &f&lMINA &2&lMEX&f&lIC&4&lANA&f&l: &c&lOFF&4&l!");
log( );

setlabel("horario","");
setlabel("efeitos","");
setlabel("rendimento","");
stop("MINA_MEXICANA");
stop();

}$$
